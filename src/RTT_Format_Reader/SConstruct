#-*-Python-*-
# Sconstruct for draco/RTT_Format_Reader
# T. M. Kelley
# Oct 28, 2008
# Input to scons
# (c) Copyright 2008 LANSLLC all rights reserved.

package_name = "RTT_Format_Reader"

# c4 source files, target, & libs
sources = [
    "Dims.cc",
    "Flags.cc",
    "CellData.cc",
    "NodeDataIDs.cc",
    "NodeData.cc",
    "Header.cc",
    "CellFlags.cc",
    "Nodes.cc",
    "Sides.cc",
    "RTT_Format_Reader.cc",
    "CellDataIDs.cc",
    "RTT_Mesh_Reader.cc",
    "SideFlags.cc",
    "CellDefs.cc",
    "SideDataIDs.cc",
    "Release.cc",
    "SideData.cc",
    "Cells.cc",
    "NodeFlags.cc",
    ]

target  = "rtt_" + package_name

# headers for export 
headers = [
    "SideFlags.hh",
    "SideDataIDs.hh",
    "Flags.hh",
    "Header.hh",
    "NodeDataIDs.hh",
    "NodeData.hh",
    "Dims.hh",
    "NodeFlags.hh",
    "Release.hh",
    "RTT_Format_Reader.hh",
    "CellFlags.hh",
    "CellDefs.hh",
    "SideData.hh",
    "Nodes.hh",
    "RTT_Mesh_Reader.hh",
    "CellDataIDs.hh",
    "Cells.hh",
    "CellData.hh",
    "Sides.hh",
    ]

# external pieces:
scons_dir = "../../../scons"
execfile(scons_dir + "/scons_master")
if arch == 'x86':
    master_suffix = "host"
else:
    master_suffix = "cell"
execfile(scons_dir + "/scons_project")
execfile(scons_dir + "/scons_master_%s" % master_suffix)

# where to build
from os.path import join
build_dir    = join(build_root,"draco",package_name)
full_target  = join(build_dir,target)
full_sources = map( lambda x: join(build_dir,x), sources)
full_headers = map( lambda x: join(build_dir,x), headers)

# install dirs

install_libdir = export_libdir
install_incdir = join(export_incdir,package_name)
install_cnfdir = join(export_incdir,package_name,package_name)

libs += [
    ]

libpaths += [
    ]

cpppaths += [
    ".",
    project_incdir,
    ]

cxxflags += ""

# load a build environment
compiler_file = "scons_compiler_%s_%s_%s" % (c4, arch, compiler)
include_guard = compiler_file + "_included"
if include_guard not in dir():
    execfile(scons_dir + "/%s" % compiler_file)

# build in the build dir
env.VariantDir(build_dir, '.')


# What to do:
# 1. configure config.h

## pass key,value pairs to the configure gen script through this dictionary.
## You'll need to supply a python module named gen_<package_name>_config; that
## module must have a function named generate_config_h.  See
## draco/src/c4/gen_c4_config for an example.
config_h_config = {
    }

env['config'] = config_h_config
import gen_RTT_Format_Reader_config 
configurator  = Builder( action = gen_RTT_Format_Reader_config.generate_config_h)
env["BUILDERS"]["GenConfig"] = configurator
config_target = env.GenConfig( join(build_dir,package_name,'config.h'),
                               'config.h.pyin')
env.Alias("configure", config_target)

# 1. build the library
lib_target = env.Library( full_target, full_sources)

# 2. install targets for library, headers
env.Install( install_libdir, lib_target)
env.Install( install_incdir, headers)
env.Install( install_cnfdir, [join(build_dir,package_name,"config.h")])

# influential alias
install_target = env.Alias( "install", [install_libdir,
                                        install_incdir,
                                        install_cnfdir])

# Default things:
env.Default( [
    config_target,
    lib_target,
    install_target
    ])

# recurse into test directory--note there is no default there
SConscript("test/SConscript")

# version

__id__ = "$Id$"

# End of file

