#-*-Python-*-
# Sconstruct for draco/c4
# T. M. Kelley
# Nov 05, 2008
# Input to scons
# (c) Copyright 2008 LANSLLC all rights reserved.

import os

package_name = "c4"

target  = "rtt_" + package_name

# c4 source files, target, & libs
sources = filter( lambda x: x[-3:] == ".cc", os.listdir('.'))

# headers for export 
headers = filter( lambda x: (x[-3:] == ".hh" or x[-2:] == ".h"),
                  os.listdir('.'))

# external pieces:
scons_dir = "../../../scons"
execfile(scons_dir + "/scons_master")
if arch == 'x86':
    master_suffix = "host"
else:
    master_suffix = "cell"
execfile(scons_dir + "/scons_project")
execfile(scons_dir + "/scons_master_%s" % master_suffix)

# where to build
from os.path import join
build_dir    = join(build_root,"draco",package_name)
full_target  = join(build_dir,target)
full_sources = map( lambda x: join(build_dir,x), sources)
full_headers = map( lambda x: join(build_dir,x), headers)

# install dirs
install_libdir = export_libdir
install_incdir = join(export_incdir,package_name)
install_cnfdir = join(export_incdir,package_name)

libs += [
    ]

libpaths += [
    ]

cpppaths += [
    ".",
    project_incdir,
    build_dir,
    ]


cxxflags += ""

# build environment

compiler_file = "scons_compiler_%s_%s_%s" % (c4, arch, compiler)
include_guard = compiler_file + "_included"
if include_guard not in dir():
    execfile(scons_dir + "/%s" % compiler_file)

env.VariantDir(build_dir, '.')

# configure config.h
config_h_config = {
    'c4_isDarwin' : isDarwin,
    'c4_isLinux' : isLinux,
    'c4_mpi'     : c4 == "mpi",
    'mpi_flavor'  : mpi_flavor,
    }

env['config'] = config_h_config
import gen_c4_config 
configurator  = Builder( action = gen_c4_config.generate_config_h)
env["BUILDERS"]["GenConfig"] = configurator
config_target = env.GenConfig( join(build_dir,package_name,'config.h'),
                               'config.h.pyin')


# What to do:
# 1. build the library
lib_target = env.Library( full_target, full_sources)

# 2. install targets for library, headers
env.Install( install_libdir, lib_target)
env.Install( install_incdir, headers)
env.Install( install_cnfdir, [join(build_dir,package_name,"config.h")])
install_target = env.Alias( "install", [install_libdir,
                                        install_incdir,
                                        install_cnfdir
                                        ])

env.Alias("configure", config_target)

Requires(lib_target, config_target)

# List of default tasks:
env.Default( [
    config_target,
    lib_target,       # build library
    install_target    # install lib, headers, config.h
    ])

# recurse into test directory--note there is no default there
SConscript("test/SConscript")

# version
__id__ = "$Id$"

# End of file

