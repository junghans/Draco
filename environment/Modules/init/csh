if ($?tcsh) then
	set modules_shell="tcsh"
else
	set modules_shell="csh"
endif
if ( $?MODULE_VERSION ) then
 	setenv MODULE_VERSION_STACK 	"$MODULE_VERSION"
else
 	setenv MODULE_VERSION		"3.2.7"
 	setenv MODULE_VERSION_STACK	"3.2.7"
endif
set exec_prefix='/ccs/codes/radtran/vendors/Linux64/Modules/$MODULE_VERSION'

set prefix=""
set postfix=""

if ( $?histchars ) then
  set histchar = `echo $histchars | cut -c1`
  set _histchars = $histchars

  set prefix  = 'unset histchars;'
  set postfix = 'set histchars = $_histchars;'
else
  set histchar = \!
endif

if ($?prompt) then
  set prefix  = "$prefix"'set _prompt="$prompt";set prompt="";'
  set postfix = "$postfix"'set prompt="$_prompt";unset _prompt;'
endif

if ($?noglob) then
  set prefix  = "$prefix""set noglob;"
  set postfix = "$postfix""unset noglob;"
endif
set postfix = "set _exit="'$status'"; $postfix; test 0 = "'$_exit;'

alias module $prefix'eval `'$exec_prefix'/bin/modulecmd '$modules_shell' '$histchar'*`; '$postfix
unset exec_prefix
unset prefix
unset postfix

setenv MODULESHOME /ccs/codes/radtran/vendors/Linux64/Modules/3.2.7

if (! $?MODULEPATH ) then
  setenv MODULEPATH `sed -n 's/[ 	#].*$//; /./H; $ { x; s/^\n//; s/\n/:/g; p; }' ${MODULESHOME}/init/.modulespath`
endif

if (! $?LOADEDMODULES ) then
  setenv LOADEDMODULES ""
endif
