
# Where are we?
set called=($_)
setenv _BINDIR $PWD
if ("$called" != "") then
    # echo "sourced $called[2]"    # the script was sourced from this location
    setenv _BINDIR `dirname "$_BINDIR/$called[2]"`
endif
if ("$0" != "tcsh") then
    # echo "run $0"                # the script was run from this location
    setenv _BINDIR `dirname "$_BINDIR/$0"`
endif
setenv DRACO_MODULE_DIR `(cd $_BINDIR/..;pwd)`


if ($?tcsh) then
	set modules_shell="tcsh"
else
	set modules_shell="csh"
endif
if ( $?MODULE_VERSION ) then
 	setenv MODULE_VERSION_STACK 	"$MODULE_VERSION"
else
  setenv MODULE_VERSION		"ccs-Linux64"
  setenv MODULE_VERSION_STACK	"ccs-Linux64"
endif
set exec_prefix='$DRACO_MODULE_DIR'
setenv MODULESHOME $DRACO_MODULE_DIR

set prefix=""
set postfix=""

if ( $?histchars ) then
  set histchar = `echo $histchars | cut -c1`
  set _histchars = $histchars

  set prefix  = 'unset histchars;'
  set postfix = 'set histchars = $_histchars;'
else
  set histchar = \!
endif

if ($?prompt) then
  set prefix  = "$prefix"'set _prompt="$prompt";set prompt="";'
  set postfix = "$postfix"'set prompt="$_prompt";unset _prompt;'
endif

if ($?noglob) then
  set prefix  = "$prefix""set noglob;"
  set postfix = "$postfix""unset noglob;"
endif
set postfix = "set _exit="'$status'"; $postfix; test 0 = "'$_exit;'

alias module $prefix'eval `'$exec_prefix'/bin/modulecmd '$modules_shell' '$histchar'*`; '$postfix
unset exec_prefix
unset prefix
unset postfix

if (! $?MODULEPATH ) then
  setenv MODULEPATH `sed -n 's/[ 	#].*$//; /./H; $ { x; s/^\n//; s/\n/:/g; p; }' ${MODULESHOME}/init/.modulespath`
  setenv MODULEPATH $MODULESHOME/modulefiles:$MODULESHOME/$MODULE_VERSION
endif

if (! $?LOADEDMODULES ) then
  setenv LOADEDMODULES ""
endif
